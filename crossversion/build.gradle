plugins {
    // need to use 0.11 for 1.16 due to https://github.com/architectury/architectury-loom/issues/91
    id "dev.architectury.loom" version "0.11.0-SNAPSHOT" apply false
}

apply plugin: 'java'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${minecraft_version}"
        mappings loom.officialMojangMappings()
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "maven-publish"

    archivesBaseName = archives_base_name
    version = mod_version
    group = maven_group

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        def targetVersion = 8
        if (JavaVersion.current().isJava9Compatible()) {
            options.release = targetVersion
        }
    }
}

task publishAllToMavenLocal {
    dependsOn subprojects.collect { it.tasks.matching { it.name == 'publishToMavenLocal' } }
}