plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false
}

architectury {
    minecraft = minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
        clientOnlyMinecraftJar()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings loom.officialMojangMappings()
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:1.18.1+build.22:v2"
        implementation project(path: ":crossversion:common", configuration: "namedElements")
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    archivesBaseName = archives_base_name
    version = mod_version
    group = maven_group

    repositories {
        // Add repositories to retrieve artifacts from in here.
        // You should only use this when depending on other mods because
        // Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
        // See https://docs.gradle.org/current/userguide/declaring_repositories.html
        // for more information about repositories.
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
        //project.logger.lifecycle("18/JavaVersion.current() = ${JavaVersion.current()} {$options.release}")
    }

    java {
        withSourcesJar()
    }
}

task buildAll {
    dependsOn subprojects.collect { it.tasks.matching { it.name == 'build' } }

    doLast {
        subprojects.forEach {
            def platproj = it
            if(platproj.name.matches("fabric|forge|quilt")) {
                copy {
                    from platproj.file("build/libs/${archives_base_name}-${mod_version}.jar")
                    into "${buildDir}/"
                    rename(".+", "${archives_base_name}-${project.name.substring(2)}-${mod_version}-${platproj.name.capitalize()}.jar")
                }
            }
        }
    }
}